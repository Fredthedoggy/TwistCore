plugins {
    id 'java'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

group = 'me.fredthedoggy'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.19.4-R0.1-SNAPSHOT"
    implementation "net.kyori:adventure-platform-bukkit:4.3.0"
    implementation "net.kyori:adventure-text-minimessage:4.13.1"
    implementation "net.kyori:adventure-api:4.13.1"
    implementation('dev.triumphteam:triumph-gui:3.1.2') {
        exclude group: 'com.google.code.gson', module: 'gson'
    }
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

shadowJar {
    relocate("net.kyori.adventure", "me.fredthedoggy.twistcore.adventure")
    relocate("net.kyori.examination", "me.fredthedoggy.twistcore.examination")
    relocate("dev.triumphteam.gui", "me.fredthedoggy.twistcore.gui.library")
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

assemble {
    dependsOn shadowJar
}